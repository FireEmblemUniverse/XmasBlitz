#include eastdlib.event
EventPointerTable(0x7, PointerList)

/*
event IDs used:
7
8
9
10
11
12
13
14 //lyn boss quote
15 //hec boss quote
20
21
22
23
24
25

*/

EscapePointPointerTable(0, Ch0EscapePoint)
Ch0EscapePoint:
EscapePoint(25, 1, NoMove)
EscapePoint(1,1,NoMove)
EscapePointEnd

VeinEffect(0, Memory_1)
VeinEffect(1, Memory_2)
VeinEffect(2, Memory_3)
VeinEffect(3, Memory_4)
VeinEffect(4, Memory_5)
VeinEffect(5, Memory_6)

//give enemies supports
SetSupportsPointer(Eliwood, EliwoodSupports)
EliwoodSupports:
SetSupportPartners(Lyn, Hector, 0, 0, 0, 0, 0)
SetSupportInitial(255,255,0,0,0,0,0)
SetSupportGrowth(0,0,0,0,0,0,0)
SetSupportNumber(2)

SetSupportsPointer(Lyn, LynSupports)
LynSupports:
SetSupportPartners(Eliwood, Hector, 0, 0, 0, 0, 0)
SetSupportInitial(255,255,0,0,0,0,0)
SetSupportGrowth(0,0,0,0,0,0,0)
SetSupportNumber(2)

SetSupportsPointer(Hector, HectorSupports)
HectorSupports:
SetSupportPartners(Lyn, Eliwood, 0, 0, 0, 0, 0)
SetSupportInitial(255,255,0,0,0,0,0)
SetSupportGrowth(0,0,0,0,0,0,0)
SetSupportNumber(2)

SetSupportsPointer(Isadora, IsadoraSupports)
IsadoraSupports:
SetSupportPartners(Harken, 0,0,0,0,0,0)
SetSupportInitial(ASupport, 0,0,0,0,0,0)
SetSupportGrowth(0,0,0,0,0,0,0)
SetSupportNumber(1)
SetSupportsPointer(Harken, HarkenSupports)
HarkenSupports:
SetSupportPartners(Isadora, 0,0,0,0,0,0)
SetSupportInitial(ASupport, 0,0,0,0,0,0)
SetSupportGrowth(0,0,0,0,0,0,0)
SetSupportNumber(1)

PointerList:
POIN TurnBasedEvents
POIN CharacterBasedEvents
POIN LocationBasedEvents
POIN MiscBasedEvents
POIN Dunno1 Dunno2 Dunno3 Tutorial
POIN Traps1 Traps2
POIN Units1 Units1  //This unit group determines the number and position of deployment slots when there is a prep screen
POIN $0 $0 $0
POIN $0 $0 $0
POIN BeginningScene EndingScene

TurnBasedEvents:
TurnEventPlayer(0, ShowTutorial, 1)
TurnEventEnemy(0, Ambush1, 4)
TurnEventEnemy(0, Ambush2, 5)
TurnEventPlayer(0, SpawnThieves, 3)
TurnEventPlayer(12, SpawnGKs, 0, 30)
TurnEventPlayer(11, SpawnRangers, 0, 30) //turn limit is only 19 anyway
TurnEventEnemy(0x9, SpawnFalcos, 0, 30) //ambush spawn yes, but they come right after the healers
TurnEventPlayer(13, SpawnFalcos2, 0, 30) //second wave falcos
// TurnEventEnemy(0, Aggro_LynHec, 10)
TurnEventEnemy(0, TelegraphAmbush, 14)
TurnEventEnemy(0, Ambush3, 15, 2)
TurnEventEnemy(0, Ambush4, 17, 2)
TurnEventPlayer(0, TimeUp, 19)
END_MAIN

CharacterBasedEvents:
CharacterEvent(0, PresentEvidence, 0, Eliwood)
END_MAIN

LocationBasedEvents:
House(7, ExamineBody,14,20)
House(7, ExamineBody, 12,20)
House(7, ExamineBody, 13, 21)
Chest(Orbs, 3,3)
Chest(MasterProof, 23, 3)
// Vendor(ShopList, 13, 24)
// Seize(15,1)
// #ifdef __DEBUG__
  // Seize(13,12)
// #endif
END_MAIN

MiscBasedEvents:
AREA 0x8 StartSpawningFalcos [0,0] [26,14]
CauseGameOverIfLordDies
END_MAIN

Dunno1:
END_MAIN

Dunno2:
END_MAIN

Dunno3:
END_MAIN

Tutorial:
END_MAIN

Units1:
UNIT Sharena EphraimLord 0x00 Level(15, Ally, 1) [13,24] 0x00 0x00 0x0 0x00 [Fensalir, Javelin, Concoction] NoAI 
UNIT Anna Hero_F 0x00 Level(15, Ally, 1) [12,24] 0x00 0x00 0x0 0x00 [Noatun, HandAxe, Concoction] NoAI 
UNIT Alfonse EirikaLord 0x00 Level(15, Ally, 1) [14,24] 0x00 0x00 0x0 0x00 [Folkvangr, LightBrand, Concoction] NoAI 
UNIT Norne Sniper_F 0x00 Level(15, Ally, 1) [11,23] 0x00 0x00 0x0 0x00 [Parthia, Longbow, Concoction] NoAI 
UNIT Mia Swordmaster_F 0x00 Level(15, Ally, 1) [15,23] 0x00 0x00 0x0 0x00 [VagueKatti, KillingEdge, Concoction] NoAI 
UNIT Narcian WyvernLord 0x00 Level(15, Ally, 1) [13,22] 0x00 0x00 0x0 0x00 [Runesword, SilverLance, Concoction] NoAI 
UNIT Linde Sage_F 0x00 Level(15, Ally, 1) [12,25] 0x00 0x00 0x0 0x00 [Aura, Lightning, Concoction] NoAI 
UNIT Amelia General_F 0x00 Level(15, Ally, 1) [14,25] 0x00 0x00 0x0 0x00 [Garm, HandAxe, Concoction] NoAI 
UNIT Olivia Dancer 0x00 Level(15, Ally, 1) [13,25] 0x00 0x00 0x0 0x00 [SilverSword, Concoction] NoAI 
UNIT Camus Paladin 0x00 Level(15, Ally, 1) [10,25] 0x00 0x00 0x0 0x00 [Gradivus, Javelin, Concoction] NoAI 
UNIT Reinhardt MageKnight 0x00 Level(15, Ally, 1) [16,25] 0x00 0x00 0x0 0x00 [DireThunder, Elfire, Concoction] NoAI 
UNIT Elise Valkyrie 0x00 Level(15, Ally, 1) [13,26] 0x00 0x00 0x0 0x00 [Physic, Rescue, Recover, Concoction] NoAI
UNIT


Bosses:
UNIT Eliwood EirikaMasterLord 0x00 Level(17, NPC, 1) [13,2] 0x00 0x00 0x0 0x00 [Durandal, Elixir] [NoAI1, 0x03, 0, GuardTile] 
UNIT Lyn Swordmaster_F Eliwood Level(17, NPC, 1) [12,2] 0x00 0x00 0x0 0x00 [ManiKatti, Elixir] GuardTileAI 
UNIT Hector General Eliwood Level(17, NPC, 1) [14,2] 0x00 0x00 0x0 0x00 [Armads, Elixir] GuardTileAI 
UNIT Lowen GreatKnight Eliwood Level(17, NPC, 1) [23,18] 0x00 0x00 0x0 0x00 [BattleAxe, Spear, Elixir] AttackInRangeAI 
UNIT Bartre Warrior Eliwood Level(17, NPC, 1) [3,18] 0x00 0x00 0x0 0x00 [BattleAxe, Longbow, Elixir] AttackInRangeAI 
UNIT Marcus Paladin Eliwood Level(17, NPC, 1) [15,3] 0x00 0x00 0x0 0x00 [SilverLance, Spear, Elixir] AttackInRangeAI 
UNIT Jaffar Assassin Eliwood Level(17, NPC, 1) [5,10] 0x00 0x00 0x0 0x00 [KillingEdge, Elixir] AttackInRangeAI 
UNIT Oswin General Eliwood Level(17, NPC, 1) [12,11] 0x00 0x00 0x0 0x00 [Spear, SilverLance, Elixir] NeverMoveAI 
UNIT Wallace General Eliwood Level(17, NPC, 1) [14,11] 0x00 0x00 0x0 0x00 [Spear, SilverLance, Elixir] NeverMoveAI 
UNIT Kent Paladin Eliwood Level(17, NPC, 1) [12,7] 0x00 0x00 0x0 0x00 [Spear, SilverSword, Elixir] PursueWithoutHeedAI 
UNIT Sain Paladin Eliwood Level(17, NPC, 1) [14,7] 0x00 0x00 0x0 0x00 [Spear, SilverLance, Elixir] PursueWithoutHeedAI 
UNIT Isadora Paladin_F Eliwood Level(17, NPC, 1) [13,16] 0x00 0x00 0x0 0x00 [LightBrand, SilverSword, Elixir] AttackInRangeAI 
UNIT Harken Hero Eliwood Level(17, NPC, 1) [13,15] 0x00 0x00 0x0 0x00 [BraveSword, Elixir] ChaseOnceApproachedAI 
UNIT Erk Sage Eliwood Level(17, NPC, 1) [21,10] 2 0x00 0x0 0x00 [Fimbulvetr, Elixir, Heal] ChaseOnceApproachedAI 
UNIT

Dorcas_Body:
UNIT Dorcas FallenPeer 0 Level(1,NPC,0) [13,20] 2 0 0 0 [Turnwheel] NeverMoveAI
UNIT

Generics:
UNIT RenewalGeneric General Eliwood Level(10, Enemy, 1) [13,11] 0x00 0x00 0x0 0x00 [SilverAxe, Tomahawk] NeverMoveAI 
// UNIT BasicGeneric General Eliwood Level(10, Enemy, 1) [8,23]  0x00 0x00 0x0 0x00 [SilverAxe, Tomahawk] NeverMoveAI 
// UNIT BasicGeneric General Eliwood Level(10, Enemy, 1) [18,23] 0x00 0x00 0x0 0x00 [SilverAxe, Tomahawk] NeverMoveAI 
UNIT BasicGeneric General Eliwood Level(10, Enemy, 1) [16,9]  0x00 0x00 0x0 0x00 [SilverAxe, Tomahawk] NeverMoveAI 
UNIT BasicGeneric General Eliwood Level(10, Enemy, 1) [10,9]   0x00 0x00 0x0 0x00 [SilverAxe, Tomahawk] NeverMoveAI 
UNIT BasicGeneric Sniper Eliwood Level(10, Enemy, 1) [1,16] 0x00 0x00 0x0 0x00 [SilverBow] AttackInRangeAI 
UNIT BasicGeneric Sniper Eliwood Level(10, Enemy, 1) [25,16] 0x00 0x00 0x0 0x00 [SilverBow] AttackInRangeAI 
UNIT BasicGeneric Hero Eliwood Level(10, Enemy, 1) [5,24] 0x00 0x00 0x0 0x00 [SilverAxe, LightBrand] AttackInRangeAI 
UNIT BasicGeneric Hero Eliwood Level(10, Enemy, 1) [21,24] 0x00 0x00 0x0 0x00 [SilverSword, Tomahawk] AttackInRangeAI 
UNIT BasicGeneric GreatKnight Eliwood Level(10, Enemy, 1) [2,20] 0x00 0x00 0x0 0x00 [BraveAxe, Javelin] PursueWithoutHeedAI 
UNIT BasicGeneric Paladin_F Eliwood Level(10, Enemy, 1) [1,20] 0x00 0x00 0x0 0x00 [BraveSword, Javelin] PursueWithoutHeedAI 
UNIT BasicGeneric GreatKnight Eliwood Level(10, Enemy, 1) [25,20] 0x00 0x00 0x0 0x00 [BraveSword, Javelin] PursueWithoutHeedAI 
UNIT BasicGeneric Paladin_F Eliwood Level(10, Enemy, 1) [24,20] 0x00 0x00 0x0 0x00 [BraveLance, Javelin] PursueWithoutHeedAI 
UNIT BasicGeneric Warrior Eliwood Level(10, Enemy, 1) [7,24] 0x02 0x00 0x0 0x00 [SilverAxe, KillerBow, MasterProof] PursueWithoutHeedAI //drop 20k feathers
UNIT BasicGeneric Warrior Eliwood Level(10, Enemy, 1) [19,24] 0x02 0x00 0x0 0x00 [KillerAxe, SilverBow, MasterProof] PursueWithoutHeedAI //drop 20k feathers
UNIT

Generics2:
UNIT BasicGeneric Paladin Eliwood Level(14, Enemy, 1)      [7,14]  0x00 0x00 0x0 0x00 [Spear, BraveSword] ChaseOnceApproachedAI 
UNIT BasicGeneric Paladin Eliwood Level(14, Enemy, 1)      [19,14] 0x00 0x00 0x0 0x00 [Spear, BraveSword] ChaseOnceApproachedAI 
UNIT BasicGeneric Paladin Eliwood Level(14, Enemy, 1)      [7,3]  0x00 0x00 0x0 0x00 [Spear, BraveSword] ChaseOnceApproachedAI 
UNIT BasicGeneric Paladin Eliwood Level(14, Enemy, 1)      [19,3]  0x00 0x00 0x0 0x00 [Spear, BraveSword] ChaseOnceApproachedAI 
UNIT CounterGeneric Sage_F Eliwood Level(14, Enemy, 1)       [6,12]  0x00 0x00 0x0 0x00 [Physic, Thunder] [HealAllies, DontMove, 0, GuardTile] 
UNIT CounterGeneric Sage_F Eliwood Level(14, Enemy, 1)       [20,12] 0x00 0x00 0x0 0x00 [Physic, Thunder] [HealAllies, DontMove, 0, GuardTile]
UNIT BasicGeneric Druid Eliwood Level(10, Enemy, 1)        [4,13]  0x00 0x00 0x0 0x00 [Nosferatu, Rescue, Recover] HealUnits 
UNIT BasicGeneric Druid Eliwood Level(10, Enemy, 1)        [22,13] 0x00 0x00 0x0 0x00 [Nosferatu, Rescue, Recover] HealUnits 
UNIT BasicGeneric Berserker Eliwood Level(14, Enemy, 1)    [3,6]   0x00 0x00 0x0 0x00 [SilverAxe, Hammer] AttackInRangeAI 
UNIT BasicGeneric Berserker Eliwood Level(14, Enemy, 1)    [23,6]  0x00 0x00 0x0 0x00 [SilverAxe, Hammer] AttackInRangeAI 
UNIT BasicGeneric Valkyrie Eliwood Level(14, Enemy, 1)     [9,2]   0x00 0x00 0x0 0x00 [Shine, Recover] HealUnits 
UNIT BasicGeneric Valkyrie Eliwood Level(14, Enemy, 1)     [17,2]  0x00 0x00 0x0 0x00 [Shine, Recover] HealUnits 
UNIT BasicGeneric Swordmaster Eliwood Level(14, Enemy, 1)  [4,6]   0x00 0x00 0x0 0x00 [SilverSword] ChaseOnceApproachedAI 
UNIT BasicGeneric Swordmaster Eliwood Level(14, Enemy, 1)  [22,6]  0x00 0x00 0x0 0x00 [SilverSword] ChaseOnceApproachedAI 
UNIT

Re_Healers:
UNIT BasicGeneric Cleric Eliwood Level(20, Enemy, 1) [1,25] 0 0 1 BL_REDA1 [Heal] HealUnits
UNIT BasicGeneric Cleric Eliwood Level(20, Enemy, 1) [25,25] 0 0 1 BR_REDA1 [Heal] HealUnits
UNIT
BL_REDA1:
REDA [2,25] 0 0 0 0 0
BR_REDA1:
REDA [24,25] 0 0 0 0 0

Rangers:
UNIT PassGeneric Ranger_F Eliwood Level(15, Enemy, 1) [1,25] 0 0 1 BL_REDA1 [Longbow] PursueWithoutHeedAI
UNIT PassGeneric Ranger Eliwood Level(15, Enemy, 1) [25,25] 0 0 1 BR_REDA1 [Longbow] PursueWithoutHeedAI
UNIT

Re_Turn4:
UNIT LungeGeneric Sage_F Eliwood Level(8, Enemy, 1) [1,25] 0 0 1 BL_REDA1 [Thunder, Lightning, Elixir] PursueWithoutHeedAI
UNIT BasicGeneric GreatKnight_F Eliwood Level(8, Enemy, 1) [1,25] 0 0 1 BL_REDA1 [SteelAxe, SteelLance, SteelSword] PursueWithoutHeedAI
UNIT LungeGeneric Druid Eliwood Level(8, Enemy, 1) [25,25] 0 0 1 BR_REDA1 [EvilEye, Thunder, Elixir] PursueWithoutHeedAI
UNIT BasicGeneric GreatKnight Eliwood Level(8, Enemy,1) [25,25] 0 0 1 BR_REDA1 [Runesword, HandAxe, ShortSpear] PursueWithoutHeedAI
UNIT

Falcos:
UNIT BasicGeneric FalcoKnight Eliwood Level(1, Enemy, 1) [1, 1] 0 0 0 0 [SteelSword] PursueWithoutHeedAI
UNIT BasicGeneric FalcoKnight Eliwood Level(1, Enemy, 1) [25, 1] 0 0 0 0 [SteelSword] PursueWithoutHeedAI
UNIT 

Falcos2:
UNIT SealSpdGeneric FalcoKnight Eliwood Level(15, Enemy, 1) [1, 1] 0 0 1 TL_REDA1 [SilverLance, Javelin] PursueWithoutHeedAI
UNIT SealStrGeneric FalcoKnight Eliwood Level(15, Enemy, 1) [25, 1] 0 0 1 TR_REDA1 [SilverSword, LightBrand] PursueWithoutHeedAI
UNIT 

Cargo:
UNIT SealDefGeneric General Eliwood Level(10, Enemy, 1) [0, 27] 0 0 0 0 [SilverAxe, Spear] PursueWithoutHeedAI
UNIT SealStrGeneric General_F Eliwood Level(10, Enemy, 1) [1, 27] 0 0 0 0 [SilverSword, Tomahawk] PursueWithoutHeedAI
UNIT

GKGroup:
UNIT BasicGeneric GreatKnight_F Eliwood Level(15, Enemy, 1) [1, 1] 0 0 1 TL_REDA1 [Wyrmslayer, Horseslayer, Hammer] PursueWithoutHeedAI
UNIT BasicGeneric GreatKnight Eliwood Level(15, Enemy, 1) [25, 1] 0 0 1 TR_REDA1 [Wyrmslayer, Horseslayer, Hammer] PursueWithoutHeedAI
UNIT 

PriestsTop:
UNIT BasicGeneric Cleric Eliwood Level(20, Enemy, 1) [1,1] 0 0 1 TL_REDA1 [Heal] HealUnits
UNIT BasicGeneric Cleric Eliwood Level(20, Enemy, 1) [25,1] 0 0 1 TR_REDA1 [Heal] HealUnits
UNIT

TL_REDA1:
REDA [1,2] 0 0 0 0 0
TR_REDA1:
REDA [25,2] 0 0 0 0 0

Thieves:
UNIT PassGeneric Thief 0 Level(10, Enemy, 1) [1,12] 2 0 0 0 [SilverSword, ChestKey] LootingThiefAI
UNIT PassGeneric Thief 0 Level(10, Enemy, 1) [25,12] 2 0 0 0 [SilverSword, ChestKey] LootingThiefAI
UNIT

Re_Healers2:
UNIT BasicGeneric Cleric Eliwood Level(20, Enemy, 1) [1,12] 0 0 1 ML_REDA1 [Heal] HealUnits
UNIT BasicGeneric Cleric Eliwood Level(20, Enemy, 1) [25,12] 0 0 1 MR_REDA1 [Heal] HealUnits
UNIT

MageKnights:
UNIT BasicGeneric MageKnight Eliwood Level(17, Enemy, 1) [1,12] 0 0 1 ML_REDA1 [Elfire] HealUnits
UNIT BasicGeneric MageKnight_F Eliwood Level(17, Enemy, 1) [1,12] 0 0 1 ML_REDA1 [Elfire] HealUnits
UNIT BasicGeneric MageKnight Eliwood Level(17, Enemy, 1) [25,12] 0 0 1 MR_REDA1 [Elfire] HealUnits
UNIT BasicGeneric MageKnight_F Eliwood Level(17, Enemy, 1) [25,12] 0 0 1 MR_REDA1 [Elfire] HealUnits
UNIT

Wyverns:
UNIT LancebreakerGeneric WyvernLord Eliwood Level(17, Enemy, 1) [1,12] 0 0 1 ML_REDA1 [SilverSword] HealUnits
UNIT AxebreakerGeneric WyvernKnight Eliwood Level(17, Enemy, 1) [1,12] 0 0 1 ML_REDA1 [KillerLance] HealUnits
UNIT LancebreakerGeneric WyvernLord Eliwood Level(17, Enemy, 1) [25,12] 0 0 1 MR_REDA1 [KillingEdge] HealUnits
UNIT AxebreakerGeneric WyvernKnight Eliwood Level(17, Enemy, 1) [25,12] 0 0 1 MR_REDA1 [SilverLance] HealUnits
UNIT

Re_Healers2:
UNIT BasicGeneric Cleric Eliwood Level(20, Enemy, 1) [1,12] 0 0 1 ML_REDA1 [Heal] HealUnits
UNIT BasicGeneric Cleric Eliwood Level(20, Enemy, 1) [25,12] 0 0 1 MR_REDA1 [Heal] HealUnits
UNIT

ML_REDA1:
REDA [1,12] 0 0 0 0 0
MR_REDA1:
REDA [25,12] 0 0 0 0 0


StartSpawningFalcos:
  SVAL 0x2 0x0
  CALL $9ee1b8 //checks allegiance
  ENUF 0x9
  CALL Aggro_LynHec
  LoadReinforcements(PriestsTop)
  NoFade
  ENDA

SpawnFalcos:
LoadReinforcements(Falcos)
LOAD1 0x1 Cargo
ENUN
Rescue(1,1,0,27)
Rescue(25,1,1,27)
ENUF 11 //next turn spawn rangers
NoFade
ENDA

SpawnFalcos2:
ReinforcementEvent(Falcos2)

SpawnRangers:
ENUF 12
ReinforcementEvent(Rangers)

SpawnGKs:
ENUF 13
ReinforcementEvent(GKGroup)

Ambush3:
ReinforcementEvent(MageKnights)

Ambush4:
ReinforcementEvent(Wyverns)

Traps1:
ENDTRAP
ALIGN 4

PrismData:
Vein(13,20,0,DVText_Memory_1)
Vein(25,15,1,DVText_Memory_1)
Vein(3,14,2,DVText_Memory_1)
Vein(13,14,3,DVText_Memory_1)
Vein(20,7,4,DVText_Memory_1)
ENDTRAP
ALIGN 4

FinalPrismData:
Vein(13,5,5,DVText_Memory_1)
ENDTRAP
ALIGN 4

Traps2:
ENDTRAP
ALIGN 4

BeginningScene:
// MUSC 0x62 //this is marc's theme lol
ENUT 0x9 //stops falcos from spawning
ENUT 11 //stop rangers from spawning
ENUT 12 //stop gks from spawning
ENUT 13 //stop second round of falcos
MUSC 0x34 //heroes theme (was combat prep)
  // REMOVEPORTRAITS
  // ASMC 0x30185 //clears map tilesets?
  // EVBIT_F 0x2
  // STAL 120
  // REMOVEPORTRAITS
  // ASMC 0x48281 //name selection
  // ClearBackgroundSmooth
  // REMA
CAM1 [13,23]
CHECK_TUTORIAL
BEQ 0x0 0xC 0x0
ENUT 0xb0 //turn on casual mode if easy
LABEL 0x0
LOAD1 0x1 Units1 //0x0 instead of 0x1 seems to be for allies on maps with prep screens
ENUN
Text(Castle_Hall, PreChapterScene1)
// Text(0x34, PreChapterScene1) //OBJECTION bg
CALL SetPlayerSkills
LOAD1 0x1 Bosses
ENUN
CALL SetBossSkills
Text(PreChapterScene2)
CAM1 Eliwood
ENUN
FlashCursor(Hector, 60)
MUSC 0xc6 //wedding music
Text(Castle_Hall_2, PreChapterScene3)
FlashCursor(Marcus, 60)
Text(Castle_Hall_2, MarcusReports)

CAM1 Anna
FlashCursor(Anna, 60)
Text(Castle_Hall_2, HeroesSplitUp)

CAM1 Eliwood
//oh no dorcas died! this is their fault!
MUSC 0x25
FlashCursor(Eliwood, 60)
Text(Castle_Hall_2, DorcasDied)
CALL TurnAllEnemies
Rescue(13,16,13,15) //isadora rescues harken
LOAD1 0x1 Dorcas_Body
ENUN
LOAD1 0x1 Generics
ENUN
LOAD1 0x1 Generics2
ENUN
// LoadReinforcements(Generics)
// LoadReinforcements(Generics2)
CAM1 [13,23]
// GotoPrepScreen
FlashCursor(Sharena, 60)
Text(Castle_Hall_2, HeroesInvestigate)
NoFade
ENDA

ShowTutorial:
CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW Turn1Tutorial
TEXTEND
REMA
FlashCursor(Dorcas, 60)
NoFade
ENDA

SpawnThieves:
LoadReinforcements(Thieves)
Text(ThievesTalk)
NoFade
ENDA

Aggro_LynHec:
CAM1 Lyn
ENUN
FlashCursor(Lyn, 60)
Text(textLynHecAggro)
SVAL 1 0
CHAI Lyn
SVAL 1 0
CHAI Hector
ENUT 4 //change music
NoFade
ENDA

TimeUp:
Text(TimeUpText)
GameOver
ENDA

Ambush1:
CAM1 Eliwood
FlashCursor(Eliwood, 60)
Text(CallInAmbush1)
LoadReinforcements(Re_Healers)
NoFade
ENDA

Ambush2:
ReinforcementEvent(Re_Turn4)

TelegraphAmbush:
ReinforcementEvent(Re_Healers2)

ExamineBody:
CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW SearchBody
TEXTEND
REMA
SVAL 3 Turnwheel
GIVEITEMTO 0xffff
SOUN 0x8d //restore staff?
FAWI 5
DISA Dorcas
AddTraps(PrismData)
STAL 16
FADU 5
ENUN
CenterTutorialTextBox
TUTORIALTEXTBOXSTART
TEXTSHOW SearchBody2
TEXTEND
REMA
FlashCursor(13,20, 60)
NoFade
ENDA

Memory_1:
MUSS 0x5d
Text(Castle_Hall, textMemory1)
MURE 2
ENUT 20
CALL CheckForFinalPrism
NoFade
ENDA

Memory_2:
MUSS 0x5D
Text(Castle_Hall, textMemory2)
MURE 2
ENUT 21
CALL CheckForFinalPrism
NoFade
ENDA

Memory_3:
MUSS 0x5D
Text(Castle_Hall, textMemory3)
MURE 2
ENUT 22
CALL CheckForFinalPrism
NoFade
ENDA

Memory_4:
MUSS 0x5D
Text(Castle_Hall, textMemory4)
MURE 2
ENUT 23
CALL CheckForFinalPrism
NoFade
ENDA

Memory_5:
MUSS 0x5D
Text(Castle_Hall, textMemory5)
MURE 2
ENUT 24
CALL CheckForFinalPrism
NoFade
ENDA

Memory_6:
MUSS 0x5D
Text(Castle_Hall, textMemoryFinal)
SETVAL 0x2 0x36; REMOVEPORTRAITS; BACG 0xFFFF;//blood pact CG lol
_0x1A22
TEXTSHOW BloodPactCGText
TEXTEND
REMA
FADI 10
Text(Castle_Hall, textMemoryFinal2)
MURE 2
ENUT 25
NoFade
ENDA

CheckForFinalPrism:
CHECK_EVENTID 20
BEQ 0x1234 r12 r0
  CHECK_EVENTID 21
  BEQ 0x1234 r12 r0
    CHECK_EVENTID 22
    BEQ 0x1234 r12 r0
      CHECK_EVENTID 23
      BEQ 0x1234 r12 r0
        CHECK_EVENTID 24
        BEQ 0x1234 r12 r0
  CALL SpawnFinalPrism
  ENUT 0x4 //change music if it didn't already
LABEL 0x1234
NoFade
ENDA

SpawnFinalPrism:
  CAM1 [13,5]
  ENUN
  FAWI 5
  AddTraps(FinalPrismData)
  SOUN 0x8d //restore staff?
  FlashCursor(13,5,60)
  STAL 16
  FADU 5
  ENUN
  NoFade
ENDA

PresentEvidence:
CHECK_EVENTID 25
BEQ 0 r12 r0
  // Text(PlaceholderText) //you solved the murder!
  CALL EndingScene
  GOTO 0x1
LABEL 0x0
Text(NotEnoughEvidence) //you didn't collect enough evidence!
LABEL 0x1
NoFade
ENDA

EndingScene:
MUSC 0x94 //examination_allegro
REMOVEPORTRAITS
BACG Castle_Hall_2
TEXTSTART
TEXTSHOW ConvincedEliwood
TEXTEND
// Text(Castle_Hall_2, ConvincedEliwood)
MUSCMID 0x7FFF
FADI 2
REMA
MUSC 0x7FFF //just in case skipping
SETVAL 0x2 0x34; REMOVEPORTRAITS; BACG 0xFFFF;//objection
_0x1A22
STAL 120
SOUN 0x92 //OBJECTION!
TEXTSTART
TEXTSHOW tAWait
TEXTEND
FADI 16
MUSCMID 0x25 //suspense
Text(Castle_Hall_2, VeronicaArrives)
ASMC prCallEventMenu+1
SVAL r7 0
BEQ 123 rC r7
  FADI 2
  MNC2 0x0 //you turned back time lol
  NoFade
  ENDA
LABEL 123
  label27:
  ASMC prEraseTurnwheel|1
  FlashCursor(Anna, 60)
  STARTFADE
  FADECOLORS 0x2000 0x8 0x80 0x80 0x80
  STAL 30
  BREAKING_SACRED_STONE Anna
  FADECOLORS 0x2000 0x4 0x100 0x100 0x100
  FAWI 2
  MNC2 0x1 //next chapter time
  NoFade
  ENDA

SetPlayerSkills:
  SetSkills(Sharena, SupplyID, RepositionID, SealStrID, SpurDefID)
  SetSkills(Alfonse, SupplyID, FieryBloodID, QuickDrawID, SpurStrID)
  SetSkills(Anna, SupplyID, VantageID, SpurResID, ShoveID)
  SetSkills(Norne, ArmoredBlowID, WardingBlowID, SealDefID,0)
  SetSkills(Mia, SureShotID, VantageID, DuelistsBlowID, 0)
  SetSkills(Narcian, AetherID, CounterMagicID, RenewalID, 0)
  SetSkills(Linde, LifeAndDeathID, DesperationID, LunaID, 0)
  SetSkills(Amelia, ProvokeID, PivotID, AegisID, 0)
  SetSkills(Olivia, SmiteID, AmaterasuID, CharmID, 0) //tadept, wings of mercy
  SetSkills(Camus, CounterMagicID, FuryID, LifetakerID, IdentityProblemsID) //fury, distant counter
  SetSkills(Reinhardt, WrathID, ImpaleID, MaleficAuraID, 0)
  SetSkills(Elise, LiveToServeID, DemoiselleID, RallySpectrumID, 0)
  NoFade
  ENDA

SetBossSkills:
  SetSkills(Eliwood, DeathBlowID, AegisID, SpurResID, 0)
  SetSkills(Lyn, AstraID,FieryBloodID,SpurSpdID,0) // dazzle: cannot be countered
  SetSkills(Hector, PaviseID, SpurStrID, StrongRiposteID, 0) //should be quick riposte
  SetSkills(Lowen, ArmoredBlowID, WardingBlowID, NaturalCoverID, 0)
  SetSkills(Marcus, ElbowRoomID, WaryFighterID, InspirationID, 0)
  SetSkills(Isadora, DartingBlowID, SpurSpdID, LancebreakerID, 0)
  SetSkills(Harken, FuryID,IgnisID,SavageBlowID,0) //add fury for frenzy
  SetSkills(Kent, StrongRiposteID, PatienceID, ChivalryID, 0)
  SetSkills(Sain, GentilhommeID, QuickDrawID, ChivalryID, 0) //reverse gentilhomme
  SetSkills(Bartre, FieryBloodID, ElbowRoomID, FrenzyID ,0)
  SetSkills(Erk, CounterID, TantivyID, CertainBlowID, 0)
  SetSkills(Oswin, WaryFighterID, CamaraderieID, StrongRiposteID, 0)
  SetSkills(Wallace, WaryFighterID, BondID, RenewalID, 0)
  SetSkills(Jaffar, PatienceID, LifetakerID, LifeAndDeathID, 0)
  NoFade
  ENDA

TurnAllEnemies:
  CUSE Eliwood 
  CUSE Lyn
  CUSE Hector
  CUSE Lowen
  CUSE Bartre
  CUSE Marcus
  CUSE Jaffar
  CUSE Oswin
  CUSE Wallace 
  CUSE Kent
  CUSE Sain
  CUSE Isadora 
  CUSE Harken
  CUSE Erk
  NoFade
  ENDA


ShopList:
SHLI MasterProof Elixir Physic
ALIGN 4  

PUSH
ORG $9ea8
SHORT 0 0 $e00a
ORG $9ed8
POIN EpilogueEvents
POP

EpilogueEvents:
SETVAL 0xB 0xC0011
LOMA 0x1 //Load some map
// MUSC 0x42
// FADU 16
// Text(TrueEnding) //here as placeholder. 15 turn clear will be required.
// FADI 3
// _0x1328 0x7FFF //fade out music?
REMOVEPORTRAITS
ASMC 0x30185 //clears map tilesets?
EVBIT_F 0x2
STAL 120
REMOVEPORTRAITS
ASMC 0xB8175 //turn counts display
REMOVEPORTRAITS
STAL 1
CALL 0xA0036C //Character Endings
STAL 90
// MUSC 0x42 //Fly with the Breeze
// ASMC 0xC4879 //Roll Credits - CGs are borked when I try to use it normally?
// STAL 90
MNTS 0x0 //return to title screen
ENDA

#define EventMenuCommand(aTextId) "WORD 0; SHORT aTextId 0; WORD 0; POIN (0x04F448+1); WORD 0; POIN (prEventMenuCommandEffect+1); WORD 0 0 0"
#define EventMenuCommandEnd "WORD 0 0 0 0 0 0 0 0 0"

prCallEventMenu:
  SHORT 0xB510 0x1C04 0x4B10 0x469E 0xF800 0x4A10 0x7850 0x2101 0x4308 0x2102 0x4308 0x2104 0x4308 0x2108 0x4308 0x2110 0x4308 0x7050 0x2000 0x4B0A 0x469E 0xF800 0x4B09 0x469E 0xF800 0x4B09 0x469E 0xF800 0x4809 0x1C21 0x4B07 0x469E 0xF800 0xBC10 0xBC01 0x4700 0xE884 0x0804 0x3080 0x0300 0x3D38 0x0800 0x43A8 0x0800 0xEB68 0x0804 0xEBC8 0x0804
  POIN pMenuDefinition

pMenuDefinition:
  BYTE 6 8 18 0           // Geomerty, order: X, Y, Width, Height (Height doesn't matter)
  WORD 1                  // idk
  POIN pCommandDefinition // Command Definitions
  WORD 0 0 0 0 0 0        // No routines to handle B press/R press/construction/etc

pCommandDefinition:
  EventMenuCommand(tDestroyTurnwheel)
  EventMenuCommand(tTurnBackTime)
EventMenuCommandEnd

prEventMenuCommandEffect:
    SHORT 0xB500 0x203C 0x5C08 0x4B03 0x469E 0xF800 0x2017 0xBC02 0x4708 0x0000 0xD1F8 0x0800

prEraseTurnwheel:
#incbin "removeturnwheel.dmp"